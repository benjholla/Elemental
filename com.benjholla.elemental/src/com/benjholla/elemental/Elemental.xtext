grammar com.benjholla.elemental.Elemental hidden(WHITESPACE, COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate elemental "http://www.benjholla.com/elemental/Elemental"

Program:
	implicitFunction=Block
	explicitFunctions+=(Function)*;
	
Block:
	{Block} instructions+=Instruction*;
	
Label:
	'"' name=ID '"';
	
Function:
	name=ID ':' body=Block;

Instruction: 
	type=(
		Increment
		| Decrement
		| MoveLeft
		| MoveRight
		| Store
		| Recall
		| Assignment
		| Branch
		| Loop
		| Label
		| GOTO
		| ComputedGOTO
		| StaticDispatch
		| DynamicDispatch
		);

Increment:
	{Increment} '+';

Decrement:
	{Decrement} '-';
	
MoveLeft:
	{MoveLeft} '<';

MoveRight:
	{MoveRight} '>';
	
Store:
	{Store} ',';
	
Recall:
	{Recall} '.';
	
Assignment:
	{Assignment} '=';	

Branch:
	{Branch} '(' body=Block ')';
	
Loop:
	{Loop} '[' body=Block ']';
	
GOTO:
	{GOTO} '\'' label=[Label] '\'';
	
ComputedGOTO:
	{ComputedGOTO} '&';

StaticDispatch:
	{StaticDispatch} '{' target=[Function] '}';
	
DynamicDispatch:
	{DynamicDispatch} '?';

terminal ID: ('0'..'9')*;

terminal COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;

terminal WHITESPACE : (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
